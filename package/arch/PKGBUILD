# Maintainer: Ong Yong Xin <ongyongxin2020+github AT gmail DOT com>
# Maintainer: Xuanrui Qi <me@xuanruiqi.com>
# Contributor: Rayfalling <Rayfalling@outlook.com>
# Contributor: facekapow, rayfalling, Ducksoft
_pkgname=genie
pkgname=${_pkgname}-systemd
pkgver=1.45
pkgrel=2
pkgdesc="A quick way into a systemd \"bottle\" for WSL"
arch=('x86_64')
url="https://github.com/arkane-systems/genie"
license=('Unlicense')
depends=('daemonize' 'dotnet-runtime>=5.0' 'dotnet-host>=5.0')
makedepends=('dotnet-sdk>=5.0')
source=(${_pkgname}-${pkgver}.tar.gz::https://github.com/arkane-systems/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('SKIP')
backup=('etc/genie.ini')
options=(!strip)

pkgver() {
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C binsrc
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  # Binaries
  install -Dm4755 binsrc/genie/bin/Release/net5.0/linux-x64/publish/genie -t ${pkgdir}/usr/lib/genie
  install -Dm0755 binsrc/runinwsl/bin/Release/net5.0/linux-x64/publish/runinwsl -t ${pkgdir}/usr/lib/genie

  # Environment generator.
  install -Dm0755 othersrc/scripts/80-genie-envar.sh -t ${pkgdir}/usr/lib/genie

  # Runtime dir mapping
  install -Dm0755 othersrc/scripts/map-user-runtime-dir.sh -t ${pkgdir}/usr/lib/genie
  install -Dm0755 othersrc/scripts/unmap-user-runtime-dir.sh -t ${pkgdir}/usr/lib/genie

  # Configuration file.
  install -Dm0644 othersrc/etc/genie.ini -t ${pkgdir}/etc

  # genie symlink
  mkdir -p ${pkgdir}/usr/bin
  ln -s /usr/lib/genie/genie ${pkgdir}/usr/bin/genie

  # 10-genie-envar.sh symlinks
  mkdir -p ${pkgdir}/usr/lib/systemd/user-environment-generators
  mkdir -p ${pkgdir}/usr/lib/systemd/system-environment-generators
  ln -s /usr/lib/genie/80-genie-envar.sh ${pkgdir}/usr/lib/systemd/user-environment-generators/80-genie-envar.sh
  ln -s /usr/lib/genie/80-genie-envar.sh ${pkgdir}/usr/lib/systemd/system-environment-generators/80-genie-envar.sh

	# Unit files.
  install -Dm0644 othersrc/lib-systemd-system/wslg-xwayland.service -t ${pkgdir}/usr/lib/systemd/system
  install -Dm0644 othersrc/lib-systemd-system/wslg-xwayland.socket -t ${pkgdir}/usr/lib/systemd/system
  mkdir -p ${pkgdir}/usr/lib/systemd/system/sockets.target.wants
  ln -s /usr/lib/systemd/system/wslg-xwayland.socket ${pkgdir}/usr/lib/systemd/system/sockets.target.wants/wslg-xwayland.socket

  install -Dm0644 othersrc/lib-systemd-system/user-runtime-dir@.service.d/override.conf -t ${pkgdir}/usr/lib/systemd/system/user-runtime-dir@.service.d

  # binfmt.d
  install -Dm0644 othersrc/usr-lib/binfmt.d/WSLInterop.conf -t ${pkgdir}/usr/lib/binfmt.d

  # man page
  install -Dm0644 othersrc/docs/genie.8 -t ${pkgdir}/usr/share/man/man8
}

# vim:set ts=2 sw=2 et:
